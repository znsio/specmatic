import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'signing'
}

apply plugin: "jacoco"
jacoco {
    toolVersion = "0.8.7"
}

repositories {
    mavenLocal()
    mavenCentral()
}

def ktor_version = "2.3.12"
def jgit_version = "6.10.0.202406032230-r"
def junit_version = "5.11.0"

dependencies {
    // TODO Fix to eliminate vulnerabilities, remove when the enclosing library supports it
    implementation 'io.netty:netty-codec-http:4.1.112.Final' // used by ktor-server-netty-jvm 2.3.9 in core
    implementation 'joda-time:joda-time:2.12.7' // used by swagger-parser 2.1.22

    implementation 'io.cucumber:gherkin:22.0.0'
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-client-core-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-apache:$ktor_version"
    implementation "io.ktor:ktor-server-cors:$ktor_version"
    implementation("io.ktor:ktor-server-double-receive:$ktor_version")

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json-jvm:1.6.3"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.9.24"
    implementation "org.eclipse.jgit:org.eclipse.jgit:$jgit_version"
    implementation "org.eclipse.jgit:org.eclipse.jgit.ssh.apache:$jgit_version"

    implementation 'com.flipkart.zjsonpatch:zjsonpatch:0.4.16'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'

    implementation 'io.swagger.parser.v3:swagger-parser:2.1.22'

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    implementation 'com.github.mifmif:generex:1.0.2'

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    testImplementation 'org.assertj:assertj-core:3.26.3'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
    testImplementation 'org.json:json:20240303'
    testImplementation 'org.springframework:spring-web:5.3.39'
    testImplementation 'io.mockk:mockk:1.13.12'
    testImplementation 'org.assertj:assertj-core:3.26.3'
    testImplementation "io.ktor:ktor-client-mock-jvm:$ktor_version"
}

test {
    useJUnitPlatform()
}

group = 'io.specmatic'

jar {
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'specmatic-core'
            from components.java
            pom {
                name = 'Specmatic'
                description = 'Turn your contracts into executable specifications. Contract Driven Development - Collaboratively Design & Independently Deploy MicroServices & MicroFrontends.'
                url = 'https://specmatic.io'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/znsio/specmatic/blob/main/License.md'
                    }
                }
                developers {
                    developer {
                        id = 'specmaticBuilders'
                        name = 'Specmatic Builders'
                        email = 'info@specmatic.io'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:znsio/specmatic.git'
                    url = 'https://specmatic.io/'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.hasProperty("ossrhUsername") ? project.getProperty("ossrhUsername") : ""
                password = project.hasProperty("ossrhPassword") ? project.getProperty("ossrhPassword") : ""
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign).configureEach {
    onlyIf { project.hasProperty("signing.keyId") }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = false
        csv.required = false
    }
}

