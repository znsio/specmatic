plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'maven-publish'
    id 'signing'
}

apply plugin: "jacoco"
jacoco {
    toolVersion = "0.8.7"
}

application {
    mainClass.set("application.SpecmaticApplication")
}

jar {
    from '../version.properties'
}

shadowJar {
    archiveFileName = "specmatic.${archiveExtension.get()}"
    from '../version.properties'
}

repositories {
    mavenLocal()
    mavenCentral()
}

def ktor_version = "1.6.3"
def junit_version = "5.8.0"
def jgit_version = "5.13.0.202109080827-r"

dependencies {
    implementation('info.picocli:picocli-spring-boot-starter:4.6.1') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
    implementation 'info.picocli:picocli:4.6.1'
    implementation "io.ktor:ktor-client-core-jvm:${ktor_version}"
    implementation "io.ktor:ktor-network-tls:$ktor_version"
    implementation "io.ktor:ktor-network-tls-certificates:$ktor_version"

    implementation 'org.junit.platform:junit-platform-launcher:1.8.0'
    implementation 'org.junit.platform:junit-platform-reporting:1.8.0'

    implementation "org.eclipse.jgit:org.eclipse.jgit:$jgit_version"
    implementation "org.eclipse.jgit:org.eclipse.jgit.ssh.apache:$jgit_version"

    implementation 'org.slf4j:slf4j-nop:1.7.32'
    implementation 'org.apache.kafka:kafka-clients:3.0.0'

    implementation 'org.apache.ant:ant-junit:1.10.11'

    implementation(project(':core'))
    implementation(project(':junit5-support'))

    implementation "io.ktor:ktor-client-cio:$ktor_version"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"

    testImplementation('org.springframework.boot:spring-boot-starter-test:2.5.4') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude module: 'mockito-core'
    }
    testImplementation 'io.mockk:mockk:1.12.0'
    testImplementation('com.ninja-squad:springmockk:3.0.1') {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
    testImplementation 'org.assertj:assertj-core:3.20.2'
    testImplementation 'com.ginsberg:junit5-system-exit:1.1.1'
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_version"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}

group = 'in.specmatic'

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
